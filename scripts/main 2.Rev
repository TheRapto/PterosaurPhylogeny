####Bayesian tree inference using fossilised birth-death process####

#Reading in the data
taxa <- readTaxonData("data/bears_taxa.tsv")
morpho <- readDiscreteCharacterData("data/bears_morphology.nex")
#Adding sequence data
cytb <- readDiscreteCharacterData("data/bears_cytb.nex")

#Making sure datasets are cross compatible
cytb.addMissingTaxa( taxa )
morpho.addMissingTaxa( taxa )

#Defining helper variables
num_taxa <- morpho.ntaxa() # number of taxa
num_branches <- 2 * num_taxa - 2 # number of branches in an rooted tree

#Variables for setting up the MCMC
moves    = VectorMoves()
monitors = VectorMonitors()

##Loading the models##
# tree model
source("scripts/FBD_dating.Rev")
# clock model
source("scripts/clock_strict_morpho.Rev")
# substitution model
source("scripts/GTR.Rev")



##Setting up the MCMC##

#Creating variable mymodel
mymodel = model(tree)

#The monitors to capture the output
# parameters printed to file
monitors.append( mnModel(filename = "output/bears_FBD_morp.log", printgen = 10, exclude = ["F"]) )
# trees printed to file
monitors.append( mnFile(filename = "output/bears_FBD_morp.trees", printgen = 10, tree) )
# parameter values printed to screen during the MCMC
monitors.append( mnScreen(printgen = 10, age_extant, num_samp_anc, origin_time) )

#Settingup the MCMC run (10000 generations)
mymcmc = mcmc(mymodel, monitors, moves)
mymcmc.run(generations = 50000, tuningInterval = 5000)

#Creates the final output
trace = readTreeTrace("output/bears_FBD_morp.trees")
mccTree(trace, file = "output/bears_FBD_morp.mcc.tre", positiveBranchLengths = TRUE)